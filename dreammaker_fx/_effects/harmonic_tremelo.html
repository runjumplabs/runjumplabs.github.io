---
layout: default
name: harmonic_tremelo
effect_type: tremelos
---
<h5>{{ page.title }} </h3>
<ul>
<li>Created by: {{page.creator}} </li>
<li>Minimum firmware ver: {{page.min_firmware}} </li>
</ul>
<pre><code class="cpp" style="arduino">#include "dm_fx.h"

/*
A harmonic tremelo is basically a pair of tremelos that are 180ยบ out of 
phase.  The first tremelo is run through a low pass filter and the second
tremelo is run through a high pass filter.  The result is a tremelo type 
sounds but the volume remains rather constant.

             +-------+    +----------------------+    +-------+
             |       |    |                      |    |       |
             | LPF   |    | Tremelo              |    |       |
         +-->+ 500Hz +--->+ (0ยบ initial phase)   +--->+       +-+
         |   |       |    |                      |    |       | |
         |   +-------+    +----------------------+    |       | |
Instr In |                                            | Mix 2 |  > Amp Out
         |   +-------+    +----------------------+    |       | |
         |   |       |    |                      |    |       | |
         +-->+ HPF   +--->+ Tremelo              +--->+       +-+
             | 500Hz |    | (180ยบ initial phase) |    |       |
             |       |    |                      |    |       |
             +-------+    +----------------------+    +-------+

 */

// Amplitude modulator #1
fx_amplitude_mod    mod1(1.0,     // Rate (Hz)
                         0.8,     // Depth (0.0->1.0)
                         0,       // Initial phase (degrees)
                         OSC_SINE,// Oscillator type
                         false);  // Don't use external LFO
// Amplitude modulator #2                         
fx_amplitude_mod    mod2(1.0,     // Rate (Hz)
                         0.8,     // Depth (0.0->1.0)
                         180,     // Initial phase (degrees)
                         OSC_SINE,// Oscillator type 
                         false);  // Don't use external LFO

// Filters for splitting low and high ends
fx_biquad_filter    lpf(500.0,              // Initial filter cutoff freq
                        FILTER_WIDTH_NARROW,// Filter Q / width
                        BIQUAD_TYPE_LPF);   // Filter type
                        
fx_biquad_filter    hpf(500.0,              // Initial filter cutoff freq
                        FILTER_WIDTH_NARROW,// Filter Q / width
                        BIQUAD_TYPE_HPF);   // Filter type


// Simple two channel mixer
fx_mixer_2    mix2;

 void setup() {

  pedal.init();

  pedal.route_audio(pedal.instr_in, lpf.input);
  pedal.route_audio(pedal.instr_in, hpf.input);
  pedal.route_audio(lpf.output, mod1.input);
  pedal.route_audio(hpf.output, mod2.input);
  
  pedal.route_audio(mod1.output, mix2.input_1);
  pedal.route_audio(mod2.output, mix2.input_2);

  pedal.route_audio(mix2.output, pedal.amp_out);

  // Optional code to print out the routing details to console
  if (true) {
    pedal.print_instance_stack();
    pedal.print_routing_table();
    pedal.print_param_tables();
  }
  
  // Run this effect
  pedal.run(); 


}


void loop() {

  static int now = millis();

  // Run pedal service to take care of stuff
  pedal.service();

  // Pot 0 changes the rate of the tremelo from 0 to 4Hz
  if (pedal.pot_0.has_changed()) {
    mod1.set_rate_hz(pedal.pot_0.val*4.0);
    mod2.set_rate_hz(pedal.pot_0.val*4.0);
  }

  // Pot 1 changes cross over frequency between 300 and 700Hz
  if (pedal.pot_1.has_changed()) {
    lpf.set_freq(500.0 + pedal.pot_1.val*400.0);
    hpf.set_freq(500.0 + pedal.pot_1.val*400.0);
  }

  // Pot 2 not working yet
  if (pedal.pot_2.has_changed()) {
  }

}

</code></pre>
